plugins {
    id 'org.springframework.boot' version '2.5.6'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'java'
    id 'jacoco'
}

group = 'at.fhv.se'
version = '1.0.0-SNAPSHOT'
sourceCompatibility = '11'

repositories {
    mavenCentral()
}

jacoco {
    toolVersion = "0.8.7"
    reportsDir = file("$buildDir/customJacocoReportDir")
}

jacocoTestReport {
    dependsOn test

    reports {
        xml.enabled false
        csv.enabled false
        html.destination file("${buildDir}/jacocoHtml")
    }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'nz.net.ultraq.thymeleaf:thymeleaf-layout-dialect'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    runtimeOnly 'com.h2database:h2'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    implementation group: 'org.apache.xmlgraphics', name: 'fop', version: '2.6'
    implementation group: 'org.postgresql', name: 'postgresql', version: '42.3.1'
    implementation 'io.cucumber:cucumber-java:7.1.0'
    testImplementation 'io.cucumber:cucumber-junit:7.1.0'
    implementation 'io.cucumber:cucumber-spring:7.1.0'
    implementation 'org.springdoc:springdoc-openapi-ui:1.6.3'
}

configurations {
    cucumberRuntime {
        extendsFrom testImplementation
    }
}

task cucumber() {
    dependsOn assemble, testClasses
    doLast {
        javaexec {
            main = "io.cucumber.core.cli.Main"
            classpath = configurations.cucumberRuntime + sourceSets.main.output + sourceSets.test.output
            args = ['--plugin', 'pretty',
                    '--plugin', 'html:build/cucumber/hotel.html',
                    '--glue', 'at.fhv.se.banking.bdd', 'src/test/resources/features']
        }
    }
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

build.dependsOn jacocoTestReport
